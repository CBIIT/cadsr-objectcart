connect

if (outcome ==success) of /deployment=objcart104.war:read-resource
undeploy objcart104.war
end-if

#1 add the jdbc module it doesn't matter if it's already there
# configure datasource
# is the driver in standalone.xml?
# if not, add
if (outcome != success) of /subsystem=datasources/jdbc-driver=mysql:read-resource 
    /subsystem=datasources/jdbc-driver=mysql:add( \
    driver-name=mysql, \
    driver-module-name=com.mysql.driver, \
    driver-xa-datasource-class-name=com.mysql.jdbc.Driver) {allow-resource-service-restart=true}
end-if

# is the db connection in standalone.xml?
# if yes remove for a chance DB information is changed
if (outcome == success) of /subsystem=datasources/data-source=ObjectCartDS:read-resource
data-source remove --name=ObjectCartDS
end-if

# is the db connection in standalone.xml?
# if not, add
if (outcome != success) of /subsystem=datasources/data-source=ObjectCartDS:read-resource
data-source add \
	--name=ObjectCartDS \
	--driver-name=mysql \
	--connection-url=jdbc:mysql://@OBJCART.DS.HOST@:@OBJCART.DS.PORT@/object_cart \
	--jndi-name=java:jboss/datasources/ObjectCartDS \
	--user-name=@OBJCART.DS.USER@ \
	--password=@OBJCART.DS.PSWD@ \
	--use-ccm=true \
	--max-pool-size=100 \ 
	--blocking-timeout-wait-millis=5000 
end-if

if (outcome != success) of /subsystem=logging/size-rotating-file-handler=OBJECTCART:read-resource
/subsystem=logging:write-attribute(name=use-deployment-logging-config, value=false)
/subsystem=logging:write-attribute(name=add-logging-api-dependencies,value=true)
/subsystem=logging/size-rotating-file-handler=OBJECTCART:add(level=@logLevel@, file={"relative-to"=>"jboss.server.log.dir", "path"=>"objectcart.log"}, append=false, autoflush=true)
/subsystem=logging/size-rotating-file-handler=OBJECTCART:write-attribute(name="formatter", value="%d [%t] %-5p [%c : %L] %m%n") 
/subsystem=logging/size-rotating-file-handler=OBJECTCART:write-attribute(name="rotate-size", value="1m")
/subsystem=logging/size-rotating-file-handler=OBJECTCART:write-attribute(name="max-backup-index", value="10")
end-if

# received from Jacob including adding a MySQL connection checker
/subsystem=datasources/data-source=ObjectCartDS:write-attribute(name=background-validation,value=true)
/subsystem=datasources/data-source=ObjectCartDS:write-attribute(name=idle-timeout-minutes,value=15)
/subsystem=datasources/data-source=ObjectCartDS:write-attribute(name=background-validation-millis,value=900000)
/subsystem=datasources/data-source=ObjectCartDS:write-attribute(name=valid-connection-checker-class-name, value=org.jboss.jca.adapters.jdbc.extensions.mysql.MySQLValidConnectionChecker)


if (outcome!= success) of /subsystem=logging/logger=gov.nih.nci.objectCart:read-resource
/subsystem=logging/logger=gov.nih.nci.objectCart:add(use-parent-handlers=false,handlers=["OBJECTCART", "CONSOLE"])
end-if

if (outcome != success) of /subsystem=undertow/servlet-container=default/setting=session-cookie:read-resource
/subsystem=undertow/servlet-container=default/setting=session-cookie:add(http-only=true,secure=true)
end-if

if (outcome == success) of /subsystem=undertow/server=default-server/host=default-host/filter-ref=server-header:read-resource
#Remove filter-ref=server-header
/subsystem=undertow/server=default-server/host=default-host/filter-ref=server-header:remove()
end-if

if (outcome == success) of /subsystem=undertow/server=default-server/host=default-host/filter-ref=x-powered-by-header:read-resource
#Remove filter-ref=x-powered-by-header
/subsystem=undertow/server=default-server/host=default-host/filter-ref=x-powered-by-header:remove()
end-if

if (outcome == success) of /subsystem=undertow/configuration=filter/response-header=server-header:read-resource
#Remove response-header=server-header
/subsystem=undertow/configuration=filter/response-header=server-header:remove()
end-if

if (outcome == success) of /subsystem=undertow/configuration=filter/response-header=x-powered-by-header:read-resource
#Remove response-header=x-powered-by-header
/subsystem=undertow/configuration=filter/response-header=x-powered-by-header:remove()
end-if